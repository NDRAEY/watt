/*
 Утилиты списка.
 | имплементация add, set, get, index_of,
 | length, delete, to_string
 | находится в
 | > crate::vm::natives::libs::natives_list
*/
unit __list_utils {
    native make -> 'list@make'
    native add -> 'list@add'
    native set -> 'list@set'
    native get -> 'list@get'
    native index_of -> 'list@index_of'
    native length -> 'list@length'
    native delete -> 'list@delete'
    native to_string -> 'list@to_string'
}

/*
 Список.
 | имплементация add, set, get, index_of,
 | length, delete, to_string
 | находится в
 | > list_utils
*/
type List {
    fun init {
        self.internal := __list_utils.make()
    }
    fun add(value) {
        __list_utils.add(self.internal, value)
    }
    fun set(index, value) {
        __list_utils.set(self.internal, index, value)
    }
    fun delete(value) {
        __list_utils.delete(self.internal, value)
    }
    fun length {
        return __list_utils.length(self.internal)
    }
    fun to_string {
        return __list_utils.to_string(self.internal)
    }
    fun get(index) {
        return __list_utils.get(self.internal, index)
    }
}

/*
 Итератор.
 | итератор вокруг списка
*/
type Iterator(list) {
    fun init {
        self.current := 0
    }
    fun has_next {
        return self.current < self.list.length()
    }
    fun next {
        if self.current >= self.list.length() {
            panic(
                'could not use next.',
                'iterator has no elements left.'
            )
        }
        value := self.list.get(self.current)
        self.current += 1
        return value
    }
}

/*
 Функция паники
 | реализация находится в
 | > crate::vm::natives::libs::natives_base
*/
native panic -> 'base@panic'

/*
 Функция _range
 | создаёт Iterator от a до b
*/
fun _range(a, b) {
    // список для итератора
    list := []
    // если a > b, то range должен быть
    // в виде [3,2,1]
    if a > b {
        n := a
        while n > b {
            list.add(n)
            n -= 1
        }
    }
    // если a < b, то range должен быть
    // в виде [1,2,3]
    elif a < b {
        n := a
        while n < b {
            list.add(n)
            n += 1
        }
    }
    // если a == b, то range должен быть
    // в виде [a] || [b]
    else {
        list.add(a)
    }
    // возвращаем итератор
    return new Iterator(list)
}